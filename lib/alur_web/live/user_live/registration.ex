defmodule AlurWeb.UserLive.Registration do
  use AlurWeb, :live_view

  alias Alur.Accounts
  alias Alur.Accounts.User

  @impl true
  def render(assigns) do
    ~H"""
    <Layouts.app flash={@flash} current_scope={@current_scope}>
      <div class="mx-auto max-w-sm space-y-6">
        <div class="text-center">
          <h1 class="text-2xl font-semibold tracking-[-0.025em] text-chalk font-display">
            Register for an account
          </h1>
          <p class="mt-2 text-sm text-fog font-text tracking-[0.025em]">
            Already registered?
            <.link navigate={~p"/users/log-in"} class="text-signal-green hover:underline font-medium">
              Log in
            </.link>
            to your account now.
          </p>
        </div>

        <div class="rounded-md bg-graphite border border-basalt p-6 shadow-subtle">
          <.form
            for={@form}
            id="registration_form"
            action={~p"/users/log-in"}
            phx-submit="save"
            phx-change="validate"
            phx-trigger-action={@trigger_submit}
            class="space-y-4"
          >
            <.input
              field={@form[:email]}
              type="email"
              label="Email"
              autocomplete="username"
              spellcheck="false"
              required
              phx-mounted={JS.focus()}
            />

            <.input
              field={@form[:password]}
              type="password"
              label="Password"
              autocomplete="new-password"
              spellcheck="false"
              required
            />

            <.button
              phx-disable-with="Creating account..."
              variant="primary"
              class="w-full"
            >
              Create an account <span aria-hidden="true">&rarr;</span>
            </.button>
          </.form>
        </div>
      </div>
    </Layouts.app>
    """
  end

  @impl true
  def mount(_params, _session, %{assigns: %{current_scope: %{user: user}}} = socket)
      when not is_nil(user) do
    {:ok, redirect(socket, to: ~p"/")}
  end

  def mount(_params, _session, socket) do
    changeset = Accounts.change_user_registration(%User{}, %{}, validate_unique: false)

    {:ok,
     socket
     |> assign(:trigger_submit, false)
     |> assign_form(changeset), temporary_assigns: [form: nil]}
  end

  @impl true
  def handle_event("save", %{"user" => user_params}, socket) do
    case Accounts.register_user(user_params) do
      {:ok, _user} ->
        {:noreply, assign(socket, :trigger_submit, true)}

      {:error, %Ecto.Changeset{} = changeset} ->
        {:noreply, assign_form(socket, changeset)}
    end
  end

  def handle_event("validate", %{"user" => user_params}, socket) do
    changeset = Accounts.change_user_registration(%User{}, user_params, validate_unique: false)
    {:noreply, assign_form(socket, Map.put(changeset, :action, :validate))}
  end

  defp assign_form(socket, %Ecto.Changeset{} = changeset) do
    form = to_form(changeset, as: "user")
    assign(socket, form: form)
  end
end
